plugins {
    alias(libs.plugins.jvm)
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.spring.contract)
    alias(libs.plugins.kotlin.spring)
    alias(libs.plugins.java.test.fixtures)
    alias(libs.plugins.sentry)
    alias(libs.plugins.git.properties)
    alias(libs.plugins.download)
    id 'maven-publish'
}

version = '0.1.1'

publishing {
    publications {
        create("position-stubs", MavenPublication) {
            artifactId = project.name
            groupId = "org.beancounter"
            version = project.version
            artifact verifierStubsJar
        }
    }
}

dependencies {
    implementation(
            platform(libs.spring.boot.dependencies),
            platform(libs.spring.cloud.dependencies),
            platform(libs.otel.bom),
            project(":jar-common"),
            project(":jar-client"),
            project(":jar-auth"),
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-logging",
            libs.sentry.openfeign,
            libs.spring.doc,
            libs.spring.doc.mvc,
            "org.springframework.boot:spring-boot-starter-aop",
            "org.springframework.cloud:spring-cloud-starter-openfeign",
            "org.springframework.boot:spring-boot-starter-security",
            "org.springframework.security:spring-security-oauth2-resource-server",
            "org.springframework.security:spring-security-oauth2-jose",
            "org.springframework.boot:spring-boot-starter-actuator",
            "org.springframework.boot:spring-boot-starter-integration",
            "org.jetbrains.kotlinx:kotlinx-coroutines-core",
            "com.fasterxml.jackson.module:jackson-module-kotlin",
            "org.jetbrains.kotlin:kotlin-reflect",
            libs.resilience4j.annotations,
            libs.resilience4j,
            "org.springframework.kafka:spring-kafka",
            "org.apache.commons:commons-math3:3.6.1"
    )
    compileOnly("org.springframework.boot:spring-boot-configuration-processor")
    contractTestImplementation("org.springframework.cloud:spring-cloud-starter-contract-verifier")
    testImplementation(
            "org.assertj:assertj-core",
            "com.fasterxml.jackson.core:jackson-databind",
            "org.springframework.boot:spring-boot-starter-test",
            "org.springframework.security:spring-security-test",
            "org.springframework.cloud:spring-cloud-contract-wiremock",
            "org.springframework.cloud:spring-cloud-contract-stub-runner",
            "org.springframework.kafka:spring-kafka-test",
            "org.junit.jupiter:junit-jupiter",
            libs.mockito.kotlin,
            testFixtures(project(":jar-auth"))
    )
    testImplementation("org.beancounter:svc-data:0.1.1:stubs") {
        transitive = false
    }
}

contracts {
    testFramework = "JUNIT5"
    testMode = 'EXPLICIT'
    baseClassForTests = "com.contracts.position.ContractVerifierBase"
}

tasks.register('pubStubs') {
    dependsOn build
    dependsOn publishToMavenLocal
}

springBoot {
    buildInfo()
}

gitProperties {
    failOnNoGitDirectory = false
}

tasks.lintKotlinContractTest {
    dependsOn generateContractTests
}

tasks.formatKotlinContractTest {
    dependsOn generateContractTests
}

bootBuildImage {
    imageName = "monowai/bc-position"
}

tasks.named("sentryCollectSourcesJava") {
    mustRunAfter("formatKotlin")
}

def isCI = System.getenv('CI')?.toBoolean() ?: false

sentry {
    if (isCI) {
        includeSourceContext = true
        org = "monowai-developments-ltd"
        projectName = "position"
        authToken = System.getenv("SENTRY_AUTH_TOKEN")
    }
}