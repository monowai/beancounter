buildscript {
    ext {
        contractsDir = file("mappings")
        stubsOutputDirRoot = file("${project.buildDir}/production/${project.name}-stubs/")
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:2.2.6.RELEASE"
    }
}

plugins {
    id "io.spring.dependency-management"
    id "org.springframework.boot"
    id "maven-publish"
    id 'org.jetbrains.kotlin.jvm'
    id 'kotlin-spring'
}
apply plugin: 'spring-cloud-contract'
apply from: "../javaCommon.gradle"

version = "0.1.1"

publishing {
    publications {
        stubs(MavenPublication) {
            groupId "org.beancounter"
            artifactId "${project.name}"
            artifact verifierStubsJar
        }
    }
}

dependencyManagement {
    imports { mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}" }
}

dependencies {
    implementation(
            project(":jar-common"),
            project(":jar-auth"),
            project(":jar-client"),
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-data-jpa",
            "org.springframework.boot:spring-boot-starter-logging",
            "org.springframework.cloud:spring-cloud-starter-openfeign",
            "org.springframework.boot:spring-boot-starter-security",
            "org.springframework.security:spring-security-oauth2-resource-server",
            "org.springframework.security:spring-security-oauth2-jose",
            "org.springframework.boot:spring-boot-starter-actuator",
            "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion",
            "io.github.resilience4j:resilience4j-spring-cloud2:$resilience4jVersion",
            "io.github.resilience4j:resilience4j-circuitbreaker:$resilience4jVersion",
            "io.github.resilience4j:resilience4j-ratelimiter:$resilience4jVersion",
            "io.github.resilience4j:resilience4j-timelimiter:$resilience4jVersion",
            "com.github.ben-manes.caffeine:caffeine",
            "com.h2database:h2", // Dev
            "org.postgresql:postgresql:42.2.19",
            "org.springframework.kafka:spring-kafka:$springKafka",
            "com.opencsv:opencsv:5.3",
    )

    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion" ) {exclude module: 'kotlin-reflect'}

    compileOnly "org.springframework.boot:spring-boot-configuration-processor"
    testImplementation(
            "org.junit.jupiter:junit-jupiter",
            "org.assertj:assertj-core",
            "org.springframework.boot:spring-boot-starter-test",
            "org.springframework.security:spring-security-test",
            'org.springframework.cloud:spring-cloud-starter-contract-verifier',
            "org.springframework.cloud:spring-cloud-contract-wiremock",
            "org.springframework.kafka:spring-kafka-test:$springKafka",
            // Not needed for this project; pulling into build cache
            "org.springframework.cloud:spring-cloud-contract-stub-runner"
            // End not strictly needed

    )


}

contracts {
    // https://cloud.spring.io/spring-cloud-contract/2.1.x/single/spring-cloud-contract.html#gradle-default-setup
    testFramework = "JUNIT5"
    testMode = "MockMvc"
    packageWithBaseClasses = "com.beancounter.marketdata.contracts"
    baseClassMappings {
        baseClassMapping(".*contract.*", "com.beancounter.marketdata.contracts.ContractVerifierBase")
    }
}

task pubStubs {
    dependsOn build
    dependsOn publishToMavenLocal
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "15"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "15"
    }
}