// Common config for a Kotlin/Bring builds

repositories {
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()
}

tasks.withType(JavaCompile) {
    options.compilerArgs.addAll('-Xlint:unchecked','-parameters')
    options.deprecation = true
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'kotlin-spring'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: "checkstyle"
apply plugin: 'jacoco'
apply plugin: 'org.jmailen.kotlinter'

compileKotlin {
    kotlinOptions {
        jvmTarget = "${sourceCompatibility}"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "${sourceCompatibility}"
    }
}

checkstyle {
    //  Property 'scope' does not exist, please check the documentation @8.42
    toolVersion = '8.41.1'
    configFile = project.file('../checkstyle.xml')
    configDirectory = file("$rootProject.projectDir/buildtools")

    configProperties = [
            'javaCommon.cache.file': "${buildDir}/javaCommon.cache",
    ]
    ignoreFailures = true
    showViolations = true
}

test {
    useJUnitPlatform()
    testLogging {
        events "failed"
    }
}
jacoco {
    toolVersion = "$jacocoVersion"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

