plugins {
  id 'io.spring.dependency-management'
  id 'org.jetbrains.kotlin.jvm'
  id 'kotlin-spring'
  id 'idea'
  id 'checkstyle'
  id 'jacoco'
  id 'org.jmailen.kotlinter'
  id 'org.springframework.boot' version "${springVersion}"
  id 'org.springframework.cloud.contract' version "${verifierVersion}"
  id 'com.gorylenko.gradle-git-properties' version "${gitProperties}"
  id 'maven-publish'
  id "io.sentry.jvm.gradle" version "4.12.0"
}
apply from: '../kotlin-spring.gradle'

version = "0.1.1"
publishing {
  publications {
    stubs(MavenPublication) {
      groupId "org.beancounter"
      artifactId "${project.name}"
      artifact verifierStubsJar
    }
  }
}

dependencyManagement {
  imports {
    mavenBom("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom:2.6.0")
  }
}

dependencies {
  implementation(
    platform("org.springframework.boot:spring-boot-dependencies:${springVersion}"),
    platform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"),
    project(":jar-common"),
    project(":jar-auth"),
    project(":jar-client"),
    "org.springframework.boot:spring-boot-starter-web",
    "org.springframework.boot:spring-boot-starter-data-jpa",
    "org.springframework.boot:spring-boot-starter-logging",
    "org.springframework.cloud:spring-cloud-starter-openfeign",
    "io.sentry:sentry-spring-boot-starter-jakarta:7.15.0",
    "io.sentry:sentry-opentelemetry-core:7.15.0",
    "io.sentry:sentry-openfeign:7.15.0",
    "io.sentry:sentry-jdbc:7.15.0",
    "org.springframework.boot:spring-boot-starter-aop",
    "org.springframework.boot:spring-boot-starter-security",
    "org.springframework.security:spring-security-oauth2-resource-server",
    "org.springframework.security:spring-security-oauth2-jose",
    "org.springframework.boot:spring-boot-starter-actuator",
    "org.springframework.boot:spring-boot-starter-integration",
    "org.jetbrains.kotlinx:kotlinx-coroutines-reactor",
    "org.jetbrains.kotlinx:kotlinx-coroutines-core",
    "io.github.resilience4j:resilience4j-spring-cloud2",
    "io.github.resilience4j:resilience4j-circuitbreaker",
    "io.github.resilience4j:resilience4j-annotations",
    "io.github.resilience4j:resilience4j-ratelimiter",
    "io.github.resilience4j:resilience4j-timelimiter",
    "com.github.ben-manes.caffeine:caffeine",
    "com.h2database:h2", // Dev
    "org.postgresql:postgresql:${postgresVersion}",
    "org.springframework.kafka:spring-kafka",
    'com.opencsv:opencsv:5.7.1',
  )

  compileOnly(
    "org.springframework.boot:spring-boot-configuration-processor"
  )
  contractTestImplementation(
    'org.springframework.cloud:spring-cloud-starter-contract-verifier',
  )
  testImplementation(
    "org.assertj:assertj-core",
    "com.fasterxml.jackson.core:jackson-databind",
    "org.springframework.boot:spring-boot-starter-test",
    "org.springframework.security:spring-security-test",
    "org.springframework.cloud:spring-cloud-contract-wiremock",
    "org.springframework.kafka:spring-kafka-test",
    "org.junit.jupiter:junit-jupiter",
    "org.junit.platform:junit-platform-suite-api",
    "org.junit.platform:junit-platform-suite-engine",
    "org.mockito.kotlin:mockito-kotlin:${mockitoKotlinVersion}",
    testFixtures(project(":jar-auth")),
  )
  implementation "org.jetbrains.kotlin:kotlin-stdlib"
}

contracts {
  testMode = 'EXPLICIT'
  failOnInProgress = false
  testFramework = "JUNIT5"
  packageWithBaseClasses = "com.contracts.data"
  baseClassForTests = "com.contracts.data.ContractVerifierBase"
}

contractTest {
  testLogging {
    exceptionFormat = 'full'
  }
}

tasks.register('testSuites', Test) {
  include '**/suites/**'
  useJUnitPlatform()
}
test {
  exclude '**/suites/**'
}
tasks.register('pubStubs') {
  dependsOn build
  dependsOn publishToMavenLocal
}

tasks.lintKotlinContractTest {
  dependsOn generateContractTests
}

tasks.formatKotlinContractTest {
  dependsOn generateContractTests
}

tasks.withType(JavaCompile).configureEach {
  options.fork = false
}

springBoot {
  buildInfo()
}

gitProperties {
  failOnNoGitDirectory = false
}

bootBuildImage {
  imageName = "monowai/bc-data"
}
kotlin {
  jvmToolchain(21)
}

def isCI = System.getenv('CI')?.toBoolean() ?: false

sentry {
  // Generates a JVM (Java, Kotlin, etc.) source bundle and uploads your source code to Sentry.
  // This enables source context, allowing you to see your source
  // code as part of your stack traces in Sentry.
  if (isCI){
    includeSourceContext = true

    org = "monowai-developments-ltd"
    projectName = "position"
    authToken = System.getenv("SENTRY_AUTH_TOKEN")
  } else {

  }
}

tasks.named('sentryCollectSourcesJava') {
  dependsOn tasks.named('generateContractTests')
  mustRunAfter tasks.named('generateContractTests')
}
