buildscript {
    ext {
        contractsDir = file("mappings")
        stubsOutputDirRoot = file("${project.buildDir}/production/${project.name}-stubs/")
        // redefine values from settings.gradle, but the vars can't be used in the dependencies closure
        springBootVersion = "2.2.1.RELEASE"
        verifierVersion = "2.2.0.RELEASE"
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:${verifierVersion}"
    }
}

plugins {
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "org.springframework.boot" version "${springBootVersion}"
    id "maven-publish"
}
apply plugin: 'spring-cloud-contract'
apply from: "../buildtools/javaCommon.gradle"
repositories {
    mavenLocal()
    maven { url "https://repo.spring.io/milestone" }
    mavenCentral()
    gradlePluginPortal()
}

version = "0.1.1"

publishing {
    publications {
        stubs(MavenPublication) {
            groupId "org.beancounter"
            artifactId "${project.name}"
            artifact verifierStubsJar
        }
    }
}

dependencies {
    implementation(
            project(":jar-common"),
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-logging",
            "org.springframework.cloud:spring-cloud-starter-openfeign",
            "org.springframework.boot:spring-boot-starter-actuator",
            "com.github.ben-manes.caffeine:caffeine",
            "org.projectlombok:lombok"
    )

    compileOnly "org.springframework.boot:spring-boot-configuration-processor"
    annotationProcessor "org.projectlombok:lombok"
    testAnnotationProcessor "org.projectlombok:lombok"

    testImplementation("io.rest-assured:rest-assured") {
        exclude(group:"com.sun.xml.bind", module:"jaxb-osgi") }

    testImplementation(
            "org.projectlombok:lombok",
            "org.assertj:assertj-core",
            "io.rest-assured:spring-mock-mvc",
            "io.rest-assured:json-schema-validator",
            'org.springframework.cloud:spring-cloud-starter-contract-verifier',
            "org.springframework.cloud:spring-cloud-contract-wiremock",
            // Not needed, but pulling into cache
            "org.springframework.cloud:spring-cloud-contract-stub-runner",
            "org.springframework.boot:spring-boot-starter-test",
            "org.junit.jupiter:junit-jupiter"
    )
}

dependencyManagement {
    imports { mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}" }
}

contracts {
    // https://cloud.spring.io/spring-cloud-contract/2.1.x/single/spring-cloud-contract.html#gradle-default-setup
//    testFramework ='JUNIT5'
    packageWithBaseClasses = 'com.beancounter.marketdata.integ'
}

idea {
    module {
        inheritOutputDirs = false
        downloadSources = true
        outputDir = file("$buildDir/classes/main/")
        testOutputDir = file("$buildDir/classes/main/")
    }
}

task pubStubs {
    dependsOn build
    dependsOn publishToMavenLocal
}
