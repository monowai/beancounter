buildscript {
    ext {
        contractsDir = file("mappings")
        stubsOutputDirRoot = file("${project.buildDir}/production/${project.name}-stubs/")
    }
    dependencies {
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:$verifierVersion"
    }
}

plugins {
    id 'io.spring.dependency-management'
    id 'org.jetbrains.kotlin.jvm'
    id 'kotlin-spring'
    id 'idea'
    id 'checkstyle'
    id 'jacoco'
    id 'org.jmailen.kotlinter'
    id "maven-publish"
    id 'org.springframework.boot' version "${springVersion}"
    id "com.gorylenko.gradle-git-properties" version "${gitProperties}"
}

apply plugin: 'spring-cloud-contract'
apply from: '../kotlin-spring.gradle'

version = '0.1.1'
publishing {
    publications {
        // https://youtrack.jetbrains.com/issue/IDEA-162281
        stubs(MavenPublication) {
            groupId "org.beancounter"
            artifactId "${project.name}"
            artifact verifierStubsJar
        }
    }
}

dependencies {
    implementation(
            platform("org.springframework.boot:spring-boot-dependencies:${springVersion}"),
            platform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"),
            project(":jar-common"),
            project(":jar-client"),
            project(":jar-auth"),
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-logging",
            "org.springframework.cloud:spring-cloud-starter-openfeign",
            "org.springframework.boot:spring-boot-starter-security",
            "org.springframework.security:spring-security-oauth2-resource-server",
            "org.springframework.security:spring-security-oauth2-jose",
            "org.springframework.boot:spring-boot-starter-actuator",
            "org.springframework.boot:spring-boot-starter-integration",
            "com.fasterxml.jackson.module:jackson-module-kotlin",
            "org.jetbrains.kotlin:kotlin-reflect",
            "io.github.resilience4j:resilience4j-spring-cloud2",
            "io.github.resilience4j:resilience4j-annotations",
            "io.github.resilience4j:resilience4j-circuitbreaker",
            "io.github.resilience4j:resilience4j-timelimiter",
            "io.github.resilience4j:resilience4j-reactor"
    )

    compileOnly(
            "org.springframework.boot:spring-boot-configuration-processor"
    )
    contractTestImplementation(
            'org.springframework.cloud:spring-cloud-starter-contract-verifier',
            'javax.inject:javax.inject:1',
    )
    testImplementation(
            "org.assertj:assertj-core",
            "com.fasterxml.jackson.core:jackson-databind",
            "org.springframework.boot:spring-boot-starter-test",
            "org.springframework.security:spring-security-test",
            "org.springframework.cloud:spring-cloud-contract-wiremock",
            "org.mockito.kotlin:mockito-kotlin:4.0.0",
            "org.springframework.cloud:spring-cloud-contract-stub-runner",
            "org.junit.jupiter:junit-jupiter",
            testFixtures(project(":jar-auth")),
    )
    testImplementation("org.beancounter:svc-data:0.1.1:stubs") {
        transitive = false
    }
}

contracts {
    testFramework = "JUNIT5"
    testMode = "MockMvc"
    baseClassForTests = "com.contracts.position.ContractVerifierBase"
}

contractTest {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
}

tasks.register('pubStubs') {
    dependsOn build
    dependsOn publishToMavenLocal
}

tasks.lintKotlinContractTest {
    dependsOn generateContractTests
}

springBoot {
    buildInfo()
}

gitProperties {
    failOnNoGitDirectory = false
}